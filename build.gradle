/*
 * Copyright (c) 2019 Interactive Information Research & Development
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


import com.proteusframework.build.GitInfo
import com.proteusframework.build.Property

// Check required properties
def props = [
    new Property(name:'repo_venturetech_username',description:'Username to access the artifact repository'),
    new Property(name:'repo_venturetech_password',description:'Password to access the artifact repository'),
    new Property(name:'publish_venturetech_username',description:'Username to upload to the artifact repository', required:false),
    new Property(name:'publish_venturetech_password',description:'Password to upload to the artifact repository', required:false),
]
props.each({it.test(rootProject)})

buildscript {
    ext.kotlin_version = version_kotlin
    repositories {
        if(hasLocalRepo){ maven { url = local_repo } }
        maven {
            url repoReleaseURL
            credentials {
                username "${repo_venturetech_username}"
                password "${repo_venturetech_password?:''}"
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.ajoberstar.grgit:grgit-core:3.1.1"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.21.0'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
//        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
        classpath "gradle.plugin.com.github.viswaramamoorthy:gradle-util-plugins:0.1.0-RELEASE"
    }

}
repositories {
//    mavenLocal()
    if(hasLocalRepo){ maven { url = local_repo } }
    maven {
        url repoReleaseURL
        credentials {
            username repo_venturetech_username
            password repo_venturetech_password
        }
        authentication {
            basic(BasicAuthentication)
        }
    }
    maven {
        url repoSnapshotURL
        credentials {
            username repo_venturetech_username
            password repo_venturetech_password
        }
        authentication {
            basic(BasicAuthentication)
        }
    }
}
GitInfo.populateProject(project)

apply from: 'gradle/plugins.gradle'
apply from: 'gradle/config.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/artifacts.gradle'
apply from: 'gradle/tests.gradle'
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/idea.gradle'


task autoDeploy(type: com.proteusframework.build.DeployTask) {
    group = 'Publishing'
    id = "${findProperty('aws_id')?:'MISSING_ID'}"
    secret ="${findProperty('aws_secret')?:'MISSING_SECRET'}"
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('bouncycastle:bcmail-jdk14:138') with module('org.bouncycastle:bcmail-jdk15on:1.52')
            substitute module('bouncycastle:bcprov-jdk14:138') with module('org.bouncycastle:bcprov-jdk15on:1.52')
            substitute module('bouncycastle:bctsp-jdk14:138') with module('org.bouncycastle:bctsp-jdk15on:1.46')
        }
        eachDependency {DependencyResolveDetails details ->
            if (details.requested.group == 'org.aspectj' && details.requested.name == 'aspectjweaver')
            {
                details.useVersion aspectjVersion
            }
            if (details.requested.group == 'xml-apis')
            {
                details.useVersion '1.4.+'
            }
        }
        // cache dynamic versions for ...
//        cacheDynamicVersionsFor 0, 'seconds'
        // cache changing modules (SNAPSHOT) for ...
//        cacheChangingModulesFor 300, 'seconds'
    }
}


